#include "totvs.ch"
#include "protheus.ch"
#include "topconn.ch"

/*{Protheus.doc} TELA02
Tela iniclal do Cadastro de Protheuzeiros
@type Function
@author Renato Silva
@since 16/05/2021
@version 1.0
@see https://tdn.totvs.com/pages/viewpage.action?pageId=23889301
@see https://tdn.totvs.com/pages/viewpage.action?pageId=24347041
@see https://tdn.totvs.com/pages/viewpage.action?pageId=23889268
@see https://tdn.totvs.com/pages/viewpage.action?pageId=6063797
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
*/
User Function tela02()
    
    Private oDlg1 as object
    Private cTitulo := "Cadastro de Protheuzeiros em TL++"

    oDlg1 := TDialog():New(0,0,549,1266,cTitulo,,,,,CLR_BLACK,CLR_WHITE,,,.T.)

    Private aFolder := {"Dados Pessoais","Endereço","Outros"}
    Private oFolder := TFolder():New(0,0,aFolder,aFolder,oDlg1,,,,.T.,,660,200)

//Variáveis da aba Dados Pessoais (Folder 01)
    Private oSayCod as object
    Private cGetCod := ""
    Private oGetCod as object

    Private oSayNome as object
    Private cGetNome := ""
    Private oGetNome as object

    Private oSayNomeRed as object
    Private cGetNomeRed := ""
    Private oGetNomeRed as object

    Private oSayDatNasc as object
    Private cGetDatNasc as date
    Private oGetDatNasc as object

    Private oSayDDD as object
    Private cGetDDD := ""
    Private oGetDDD as object

    Private oSayCel as object
    Private cGetCel := ""
    Private oGetCel as object

    Private oGroupSexo as object
    Private aSexo := {"Masculino","Feminino"}
    Private nSexo := "1"
    Private oRadioSexo as object

    Private oSayCPF as object
    Private cGetCPF := ""
    Private oGetCPF as object


//Variáveis da aba Endereço (Folder 02)
    Private oSayEnde as object
    Private cGetEnde := ""
    Private oGetEnde as object

    Private oSayBair as object
    Private cGetBair := ""
    Private oGetBair as object

    Private oSayImov as object
    Private aImovel := {"Casa","Apartamento","Outro"}
    Private cImovel := aImovel[2]
    Private oComboImov as object

    Private oSayCEP as object
    Private cGetCEP := ""
    Private oGetCEP as object
    
    Private oSayEst as object
    Private aEstado := {"AC","AL","AM","AP","BA","CE","DF","ES","GO","MA","MG","MS","MT","PA","PB","PE","PI","PR","RJ","RO","RN","RR","RS","SC","SE","SP","TO"}
    Private cGetEst := aEstado[1]
    Private oGetEst as object


//Variáveis da aba Outros (Folder 03)
    Private oSayEstCvl as object
    Private aEstCiv := {"Casado","Solteiro","Namorando AdvPL","Casado com Protheus","Pai tah oh"}
    Private cGetEstCvl := aEstCiv[2]
    Private oGetEstCvl as object

    Private oGrupoPet as object    
    Private aPet := {"Sim","Não"}
    Private nPet := 2
    Private oRadioPet as object

    Private oGrupoLing as object    
    Private oCheckAdvpl as object
    Private oCheckSql as object
    Private oCheckCSharp as object
    Private oCheckPython as object
    Private lCheckAdvpl  := .F.
    Private lCheckSql    := .F.
    Private lCheckCSharp := .F.
    Private lCheckPython := .F.
    Private oButtonTodas as object
    Private oButtonNenhuma as object

    Private oSayObserv as object
    Private cObserv := ""
    Private oMultGet as object


//Variáveis fora dos folders
    Private oButtonIncluir,oButtonAlterar,oButtonConsult,oButtonExcluir,oButtonSair,oButtonLimparTela as object
    Private oBitmapIncluir,oBitmapAlterar,oBitmapConsult,oBitmapExcluir,oBitmapSair,oBitmapLimparTela as object

//Elementos visuais da aba Dados Pessoais (Folder 01)
    oSayCod := TSay():New(01,05,{ ||"Código"},oFolder:aDialogs[1],,,,,,.T.,CLR_RED,CLR_WHITE,300,20)
    cGetCod := space(6)
    oGetCod := TGet():New(11,05,{|u|iif(PCount()>0,cGetCod:=u,cGetCod)},oFolder:aDialogs[1],15,10,"@E 999999",,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,cGetCod,,,,)

    oSayNome := TSay():New(25,05,{ ||"Nome"},oFolder:aDialogs[1],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,20)
    cGetNome := space(30)
    oGetNome := TGet():New(35,05,{|u|iif(PCount()>0,cGetNome:=u,cGetNome)},oFolder:aDialogs[1],75,10,"@!",,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,cGetNome,,,,)

    oSayNomeRed := TSay():New(25,110,{ ||"Nome Reduzido"},oFolder:aDialogs[1],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,20)
    cGetNomeRed := space(20)
    oGetNomeRed := TGet():New(35,110,{|u|iif(PCount()>0,cGetNomeRed:=u,cGetNomeRed)},oFolder:aDialogs[1],50,10,"",,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,cGetNomeRed,,,,)

    oSayDatNasc := TSay():New(25,180,{ ||"Data de Nascimento"},oFolder:aDialogs[1],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,20)
    cGetDatNasc := Date()
    oGetDatNasc := TGet():New(35,180,{|u|iif(PCount()>0,cGetDatNasc:=u,cGetDatNasc)},oFolder:aDialogs[1],45,10,"",,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,,,,,.T.)

    oSayDDD := TSay():New(25,250,{ ||"DDD"},oFolder:aDialogs[1],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,20)
    cGetDDD := space(4)
    oGetDDD := TGet():New(35,250,{|u|iif(PCount()>0,cGetDDD:=u,cGetDDD)},oFolder:aDialogs[1],20,10,"@E (99)",,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,cGetDDD,,,,)

    oSayCel := TSay():New(25,280,{ ||"Celular"},oFolder:aDialogs[1],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,20)
    cGetCel := space(11)
    oGetCel := TGet():New(35,280,{|u|iif(PCount()>0,cGetCel:=u,cGetCel)},oFolder:aDialogs[1],40,10,"@E 9-9999-9999",,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,cGetCel,,,,)

    oGroupSexo := TGroup():New(25,330,60,430,"Sexo",oFolder:aDialogs[1],,,.T.)
    oRadioSexo := TRadMenu():New(35,340,aSexo,{|u|iif(PCount()>0,nSexo:=u,nSexo)},oFolder:aDialogs[1],,,,,,,,60,10,,,,.T.)

    oSayCPF := TSay():New(50,05,{ ||"C.P.F."},oFolder:aDialogs[1],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,20)
    cGetCPF := space(14)
    oGetCPF := TGet():New(60,05,{|u|iif(PCount()>0,cGetCPF:=u,cGetCPF)},oFolder:aDialogs[1],60,10,"@E 999.999.999-99",,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,cGetCPF,,,,)

//Elementos Visuais da aba Endereço (Folder 02)
    oSayEnde := TSay():New(01,05,{ ||"Endereço"},oFolder:aDialogs[2],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,20)
    cGetEnde := space(30)
    oGetEnde := TGet():New(10,05,{|u|iif(PCount()>0,cGetEnde:=u,cGetEnde)},oFolder:aDialogs[2],96,10,"",,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,cGetEnde,,,,)

    oSayBair := TSay():New(01,110,{ ||"Bairro"},oFolder:aDialogs[2],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,20)
    cGetBair := space(20)
    oGetBair := TGet():New(10,110,{|u|iif(PCount()>0,cGetBair:=u,cGetBair)},oFolder:aDialogs[2],60,10,"",,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,cGetBair,,,,)

    oSayImov := TSay():New(01,180,{ ||"Tipo de Imóvel"},oFolder:aDialogs[2],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,20)
    oComboImov := TComboBox():New(10,180,{|u|iif(PCount()>0,cImovel:=u,cImovel)},aImovel,60,10,oFolder:aDialogs[2],,,,,,.T.,,,,,,,,,"cImovel")
    
    oSayCEP := TSay():New(01,250,{ ||"CEP"},oFolder:aDialogs[2],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,20)
    cGetCEP := space(9)
    oGetCEP := TGet():New(10,250,{|u|iif(PCount()>0,cGetCEP:=u,cGetCEP)},oFolder:aDialogs[2],40,10,"@E 99999-999",,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,cGetCEP,,,,)
    
    oSayEst := TSay():New(30,05,{ ||"Estado"},oFolder:aDialogs[2],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,20)
    oComboEst := TComboBox():New(40,05,{|u|iif(PCount()>0,cGetEst:=u,cGetEst)},aEstado,30,10,oFolder:aDialogs[2],,,,,,.T.,,,,,,,,,"cGetEst")
    
    oSayCid := TSay():New(30,50,{ ||"Cidade"},oFolder:aDialogs[2],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,20)
    cGetCid := space(30)
    oGetCid := TGet():New(40,50,{|u|iif(PCount()>0,cGetCid:=u,cGetCid)},oFolder:aDialogs[2],100,10,"",,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,cGetCid,,,,)


//Elementos Visuais da aba Outros (Folder 03)
    oSayEst := TSay():New(01,05,{ ||"Estado Civil"},oFolder:aDialogs[3],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,20)
    oComboEst := TComboBox():New(10,05,{|u|iif(PCount()>0,cGetEstCvl:=u,cGetEstCvl)},aEstCiv,65,10,oFolder:aDialogs[3],,,,,,.T.,,,,,,,,,"cGetEstCvl")
    
    oGrupoPet := TGroup():New(01,80,40,140,"Possui Pet?",oFolder:aDialogs[3],,,.T.)
    oRadioSexo := TRadMenu():New(10,85,aPet,{|u|iif(PCount()>0,nPet:=u,nPet)},oFolder:aDialogs[3],,,,,,,,30,10,,,,.T.)

    oGrupoLing := TGroup():New(01,150,55,250,"Linguagens que domina:",oFolder:aDialogs[3],,,.T.)
    oCheckAdvpl := TCheckBox():New(10,160,"ADVPL",{|| lCheckAdvpl},oFolder:aDialogs[3],100,210,,{|| lCheckAdvpl:=!lCheckAdvpl},,,,,,.T.,,,)
    oCheckSql := TCheckBox():New(20,160,"SQL",{|| lCheckSql},oFolder:aDialogs[3],100,210,,{|| lCheckSql:=!lCheckSql},,,,,,.T.,,,)
    oCheckCSharp := TCheckBox():New(30,160,"C#",{|| lCheckCSharp},oFolder:aDialogs[3],100,210,,{|| lCheckCSharp:=!lCheckCSharp},,,,,,.T.,,,)
    oCheckPython := TCheckBox():New(40,160,"Python",{|| lCheckPython},oFolder:aDialogs[3],100,210,,{|| lCheckPython:=!lCheckPython},,,,,,.T.,,,)
    oButtonTodas := TButton():New(15,200,"Todas",oFolder:aDialogs[3],{|| lCheckAdvpl:=.T.,lCheckSql:=.T.,lCheckCSharp:=.T.,lCheckPython:=.T.},40,10,,,.F.,.T.,.F.,,.F.,,,.F.)
    oButtonNenhuma := TButton():New(35,200,"Nenhuma",oFolder:aDialogs[3],{|| lCheckAdvpl:=.F.,lCheckSql:=.F.,lCheckCSharp:=.F.,lCheckPython:=.F.},40,10,,,.F.,.T.,.F.,,.F.,,,.F.)

    oSayObserv := TSay():New(01,270,{ ||"Observação"},oFolder:aDialogs[3],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,20)
    cObserv := "Digite aqui as características de Protheuzeiro que te definem"
    oMultGet := TMultiGet():New(10,270,{|u|iif(PCount()>0,cObserv:=u,cObserv)},oFolder:aDialogs[3],200,100,,,,,,.T.)

//Elementos visuais fora dos folders
    oBitmapIncluir := TBitmap():New(220,030,142,142,,"C:\TOTVS\PROTHEUS12_LG\Protheus_Data\system\images\incluirprotheuzeiro.BMP"  ,.T., oDlg1,{||Alert("Clique em Incluir")}  ,,.F.,.F.,,,.F.,,.T.,,.F.)
    oBitmapIncluir:lAutoSize:=.T.
    oBitmapAlterar := TBitmap():New(220,090,142,142,,"C:\TOTVS\PROTHEUS12_LG\Protheus_Data\system\images\alterarprotheuzeiro.BMP"  ,.T., oDlg1,{||Alert("Clique em Alterar")}  ,,.F.,.F.,,,.F.,,.T.,,.F.)
    oBitmapAlterar:lAutoSize:=.T.
    oBitmapConsult := TBitmap():New(220,150,142,142,,"C:\TOTVS\PROTHEUS12_LG\Protheus_Data\system\images\consultarprotheuzeiro.BMP",.T., oDlg1,{||Alert("Clique em Consultar")},,.F.,.F.,,,.F.,,.T.,,.F.)
    oBitmapConsult:lAutoSize:=.T.
    oBitmapExcluir := TBitmap():New(220,210,142,142,,"C:\TOTVS\PROTHEUS12_LG\Protheus_Data\system\images\excluirprotheuzeiro.BMP"  ,.T., oDlg1,{||Alert("Clique em Excluir")}  ,,.F.,.F.,,,.F.,,.T.,,.F.)
    oBitmapExcluir:lAutoSize:=.T.
    oBitmapLimparTela := TBitmap():New(220,305,142,142,,"C:\TOTVS\PROTHEUS12_LG\Protheus_Data\system\images\limpartela.BMP"  ,.T., oDlg1,{||limpatela()},,.F.,.F.,,,.F.,,.T.,,.F.)
    oBitmapLimparTela:lAutoSize:=.T.    
    oBitmapSair := TBitmap():New(220,365,142,142,,"C:\TOTVS\PROTHEUS12_LG\Protheus_Data\system\images\sair.BMP"  ,.T., oDlg1,{||FWAlertWarning("Obrigado por utilizar nosso produto","GRATIDÃO"),oDlg1:End()}  ,,.F.,.F.,,,.F.,,.T.,,.F.)
    oBitmapSair:lAutoSize:=.T.

    oButtonIncluir    := TButton():New(250,020,"Incluir",  oDlg1,{|| incluirSZA()},40,15,,,.F.,.T.,.F.,,.F.,,,.F.)
    oButtonAlterar    := TButton():New(250,080,"Alterar",  oDlg1,{|| alterarSZA()},40,15,,,.F.,.T.,.F.,,.F.,,,.F.)
    oButtonConsult    := TButton():New(250,140,"Consultar",oDlg1,{|| buscarSZA()},40,15,,,.F.,.T.,.F.,,.F.,,,.F.)
    oButtonExcluir    := TButton():New(250,200,"Excluir",  oDlg1,{|| deletarSZA()},40,15,,,.F.,.T.,.F.,,.F.,,,.F.)
    oButtonLimparTela := TButton():New(250,300,"Limpar Tela",oDlg1,{||limpatela()},40,15,,,.F.,.T.,.F.,,.F.,,,.F.)
    oButtonSair       := TButton():New(250,360,"Sair",  oDlg1,{|| oDlg1:End()},40,15,,,.F.,.T.,.F.,,.F.,,,.F.)

    oDlg1:Activate(,,,.T.)

Return


/*/{Protheus.doc} IncluirSZA
    Utilização da função RecLock() para a inclusão de registros
    @type  Function
    @author Renato Silva
    @since 16/05/2021
    @version 1.0
    @see https://tdn.totvs.com/display/tec/Empty
    /*/
Static Function IncluirSZA()
    DBSelectArea("SZA")
    DBSetOrder(1)
    If DBSeek(xFilial("SZA")+cGetCod)
        FWAlertWarning("O código escolhido já existe.", "Inclusão Negada")
    Else
        If ( Empty(cGetNome) .OR. Empty(cGetNomeRed) .OR. Empty(cGetDatNasc) .OR. Empty(StrTran(StrTran(cGetDDD,"(",""),")","")) .OR. Empty(StrTran(cGetCel,"-","")) .OR. Empty(StrTran(StrTran(cGetCPF,"."),"-","")) )	   
            FWAlertWarning("Existem campos obrigatórios na primeira aba que não foram preenchidos"+CHR(13)+CHR(10)+"Por favor, verifique!","ATENÇÃO")
        Else    
            RecLock("SZA",.T.)
            //Dados Pessoais
                SZA->ZA_FILIAL  := xFilial("SZA")
                SZA->ZA_COD     := cGetCod
                SZA->ZA_NOME    := cGetNome
                SZA->ZA_NOMERED := cGetNomeRed
                SZA->ZA_DATNASC := cGetDatNasc
                SZA->ZA_DDD     := StrTran(StrTran(cGetDDD,"(",""),")","")
                SZA->ZA_CELULAR := StrTran(cGetCel,"-","")
                SZA->ZA_SEXO    := cValToChar(nSexo)            
                SZA->ZA_CPF     := StrTran(StrTran(cGetCPF,"."),"-","")
            //Endereço
                SZA->ZA_ENDEREC := cGetEnde
                SZA->ZA_BAIRRO  := cGetBair
                SZA->ZA_TPOIMOV := cImovel
                SZA->ZA_CEP     := StrTran(cGetCEP,"-","")
                SZA->ZA_ESTADO  := cGetEst            
                SZA->ZA_CIDADE  := cGetCid
            //Outros
                SZA->ZA_ESTCIVI := cGetEstCvl
                SZA->ZA_TEMPET  := cValToChar(nPet)
                SZA->ZA_ADVPL   := IIF(lCheckAdvpl,"S","N")
                SZA->ZA_SQL     := IIF(lCheckSql,"S","N")
                SZA->ZA_CSHARP  := IIF(lCheckCSharp,"S","N")            
                SZA->ZA_PYTHON  := IIF(lCheckPython,"S","N")
                SZA->ZA_OBS     := cObserv
            MSUnlock()
            
            If nSexo = 1
                FWAlertInfo("Protheuzeiro incluido com sucesso", "SUCESSO")
            Else
                FWAlertInfo("Protheuzeira incluida com sucesso", "SUCESSO")
            EndIf
            
            LimpaTela()

        EndIf

    EndIf

    SZA->(DBCloseArea())

Return


Static Function BuscarSZA()
    DBSelectArea("SZA")
    DBSetOrder(1)
    If DBSeek(xFilial("SZA")+cGetCod)   // Buscar pelo código digitado no TGet
        If SZA->ZA_SEXO = '1'
            FWAlertInfo("Protheuzeiro localizado","CONSULTA")
        Else
            FWAlertInfo("Protheuzeira localizada","CONSULTA")
        EndIf
    //Dados Pessoais
        cGetNome    := SZA->ZA_NOME
        cGetNomeRed	:= SZA->ZA_NOMERED 
        cGetDatNasc	:= SZA->ZA_DATNASC 
        cGetDDD	    := "("+SZA->ZA_DDD+")"     
        cGetCel	        := SubStr(SZA->ZA_CELULAR,1,1)+"-"+SubStr(SZA->ZA_CELULAR,2,4)+"-"+SubStr(SZA->ZA_CELULAR,6,4) 
        nSexo           := Val(SZA->ZA_SEXO)
        cGetCPF	        := SubStr(SZA->ZA_CPF,1,3)+"."+SubStr(SZA->ZA_CPF,4,3)+"."+SubStr(SZA->ZA_CPF,7,3)+"-"+SubStr(SZA->ZA_CPF,10,2)     
    //Endereço
        cGetEnde	    := SZA->ZA_ENDEREC 
        cGetBair	    := SZA->ZA_BAIRRO  
        cImovel	        := SZA->ZA_TPOIMOV 
        cGetCEP	        := SubStr(SZA->ZA_CEP,1,5)+"-"+SubStr(SZA->ZA_CEP,6,3)     
        cGetEst         := SZA->ZA_ESTADO  
        cGetCid	        := SZA->ZA_CIDADE  
    //Outros
        cGetEstCvl	    := SZA->ZA_ESTCIVI 
        nPet	        := Val(SZA->ZA_TEMPET)  
        lCheckAdvpl	    := IIF(SZA->ZA_ADVPL="S",.T.,.F.)   
        lCheckSql	    := IIF(SZA->ZA_SQL="S",.T.,.F.)     
        lCheckCSharp    := IIF(SZA->ZA_CSHARP="S",.T.,.F.)  
        lCheckPython	:= IIF(SZA->ZA_PYTHON="S",.T.,.F.)  
        cObserv	        := SZA->ZA_OBS
    Else
        FWAlertError("Protheuzeiro não localizado","CONSULTA")
    EndIf 
    SZA->(DBCloseArea())
Return


Static Function AlterarSZA()
    DBSelectArea("SZA")
    DBSetOrder(1)
    If ( !Empty(cGetNome) .AND. !Empty(cGetNomeRed) .AND. !Empty(cGetDatNasc) .AND. !Empty(StrTran(StrTran(cGetDDD,"(",""),")","")) .AND. !Empty(StrTran(cGetCel,"-","")) .AND. !Empty(StrTran(StrTran(cGetCPF,"."),"-","")) )	   
        If DBSeek(xFilial("SZA")+cGetCod)
            RecLock("SZA",.F.)
        //Dados Pessoais
            SZA->ZA_NOME    := cGetNome
            SZA->ZA_NOMERED := cGetNomeRed
            SZA->ZA_DATNASC := cGetDatNasc
            SZA->ZA_DDD     := StrTran(StrTran(cGetDDD,"(",""),")","")
            SZA->ZA_CELULAR := StrTran(cGetCel,"-","")
            SZA->ZA_SEXO    := cValToChar(nSexo)            
            SZA->ZA_CPF     := StrTran(StrTran(cGetCPF,"."),"-","")
        //Endereço
            SZA->ZA_ENDEREC := cGetEnde
            SZA->ZA_BAIRRO  := cGetBair
            SZA->ZA_TPOIMOV := cImovel
            SZA->ZA_CEP     := StrTran(cGetCEP,"-","")
            SZA->ZA_ESTADO  := cGetEst            
            SZA->ZA_CIDADE  := cGetCid
        //Outros
            SZA->ZA_ESTCIVI := cGetEstCvl
            SZA->ZA_TEMPET  := cValToChar(nPet)
            SZA->ZA_ADVPL   := IIF(lCheckAdvpl,"S","N")
            SZA->ZA_SQL     := IIF(lCheckSql,"S","N")
            SZA->ZA_CSHARP  := IIF(lCheckCSharp,"S","N")            
            SZA->ZA_PYTHON  := IIF(lCheckPython,"S","N")
            SZA->ZA_OBS     := cObserv
            MSUnlock()
        
            If nSexo = 1
                FWAlertInfo("Protheuzeiro alterado","CONSULTA")
            Else
                FWAlertInfo("Protheuzeira alterada","CONSULTA")
            EndIf
        
        EndIf
    Else
        FWAlertWarning("Existem campos obrigatórios na primeira aba que não foram preenchidos"+CHR(10)+CHR(13)+"Por favor, verifique!","ATENÇÃO")    
    EndIf
    SZA->(DBCloseArea())
Return


Static Function DeletarSZA()
    If FWAlertYesNo("Deseja mesmo excluir o registro de "+cGetNome+"?","ATENÇÃO")
        DBSelectArea("SZA")
        DBSetOrder(1)
        If DBSeek(xFilial("SZA")+cGetCod)
            RecLock("SZA",.F.)
            DBDelete()
            MSUnlock()
        
            If nSexo = 1
                FWAlertInfo("Protheuzeiro excluído","SUCESSO")
            Else
                FWAlertInfo("Protheuzeira excluída","SUCESSO")
            EndIf

        EndIf
        SZA->(DBCloseArea())

        LimpaTela()
    Else
        FWAlertInfo("Exclusão abortada pelo usuário","ATENÇÃO")
    EndIf
Return


Static Function LimpaTela()
    cGetCod         := space(6)
    cGetNome	    := space(30)    
    cGetNomeRed	    := space(20)
    cGetDatNasc	    := Date()
    cGetDDD	        := space(4)     
    cGetCel	        := space(11)
    nSexo           := 1    
    cGetCPF	        := space(14)
    cGetEnde	    := space(30)
    cGetBair	    := space(20)
    cImovel	        := aImovel[2]
    cGetCEP	        := space(9)
    cGetEst         := aEstado[1] 
    cGetCid	        := space(30)
    cGetEstCvl	    := aEstCiv[2] 
    nPet	        := 2 
    lCheckAdvpl	    := .F.   
    lCheckSql	    := .F.     
    lCheckCSharp    := .F.  
    lCheckPython	:= .F. 
    cObserv	        := "Digite aqui as características de protheuzeiro que te definem."
Return
