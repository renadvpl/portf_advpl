//Bibliotecas
#Include "TOTVS.ch"
#Include "TopConn.ch"
#Include "RPTDef.ch"
#Include "FWPrintSetup.ch"

//Alinhamentos
#Define PAD_LEFT    0
#Define PAD_RIGHT   1
#Define PAD_CENTER  2
#Define PAD_JUSTIFY 3

/*
%%%%%%%%%%%%%%%% {Protheus.doc} User Function PDF003 %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%    @Desc:   Geração de PDF com FWMSPrinter (impressão de cabecalho c/ cores)        %%
%%    @Func:   PDF003                                                                  %%
%%    @Author: Renato Silva                                                            %%
%%    @Since:  28/11/2022                                                              %%
%%    @obs     https://tdn.totvs.com/display/public/framework/FWMsPrinter              %%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
*/

User Function PDF003()

    Local aArea  := FWGetArea()

    If FWAlertYesNo("Deseja gerar o relatório?", "ATENÇÃO")
        Processa({|| fMontaPDF()}, "Processando...")
    EndIf

    FWRestArea(aArea)

Return NIL

 
Static Function fMontaPDF()

    Local cCaminho    := ""
    Local cArquivo    := ""
    Local lNegrito    := .T.
    Local lSublinhado := .T.
    Local lItalico    := .T.
    Local nRegAtu     := 0
    Local nRegTot     := 0
 
    Private nLinAtu   := 000
    Private nTamLin   := 010
    Private nLinFin   := 820
    Private nColIni   := 010
    Private nColFin   := 550
    Private nColMeio  := (nColFin-nColIni)/2
    Private nEspLin   := 015

    //Colunas dos campos
    Private nColProd  := nColIni
    Private nColDescr := nColIni + 060
    Private nColTipo  := nColFin - 280
    Private nColTpDes := nColFin - 240
    Private nColUnMed := nColFin - 140
    Private nColUMDes := nColFin - 100

    //Cores usadas
    Private nCorFraca := RGB(204, 236, 255)
    Private nCorForte := RGB(000, 032, 096)
    Private nCorCinza := RGB(150, 150, 150)
    Private oBrush    := TBrush():New(, nCorFraca)

    Private dDataGer  := Date()
    Private cHoraGer  := Time()
    Private nPagAtu   := 1

    Private oPrintPvt
    Private cNomeFont  := "Arial"
    Private oFontCabN  := TFont():New(cNomeFont, , -15, ,  lNegrito, , , , , !lSublinhado, !lItalico)
    Private oFontDet   := TFont():New(cNomeFont, , -11, , !lNegrito, , , , , !lSublinhado, !lItalico)
    Private oFontDetN  := TFont():New(cNomeFont, , -13, ,  lNegrito, , , , , !lSublinhado, !lItalico)
    Private oFontDetI  := TFont():New(cNomeFont, , -11, , !lNegrito, , , , , !lSublinhado,  lItalico)
    Private oFontMin   := TFont():New(cNomeFont, , -09, , !lNegrito, , , , , !lSublinhado, !lItalico)
    Private oFontRod   := TFont():New(cNomeFont, , -08, , !lNegrito, , , , , !lSublinhado, !lItalico)
    
    cCaminho  := GetTempPath()
    cArquivo  := "PDF003_" + DtoS(dDataGer) + "_" + StrTran(cHoraGer, ':', '-')
        
    oPrintPvt := FWMSPrinter():New(cArquivo,IMP_PDF,.F.,"",.T.,,,"",,,,.T.)
    oPrintPvt:cPathPDF := cCaminho
     
    oPrintPvt:SetResolution(72)
    oPrintPvt:SetPortrait()
    oPrintPvt:SetPaperSize(DMPAPER_A4)
    oPrintPvt:SetMargin(60, 60, 60, 60)

    //Imprime o primeiro cabeçalho
    fImpCab()

    //Percorre cem registros para testar a quebra
    nRegTot := 200
    For nRegAtu := 1 To nRegTot
        IncProc("Imprimindo registro (" + cValToChar(nRegAtu) + " de " + cValToChar(nRegTot) + ")...")

        //Verifica a quebra de página
        fQuebra()

        //Aciona a impressão dos dados
        oPrintPvt:SayAlign(nLinAtu, nColProd  ,  "AAAA"  ,   oFontDet,  060,  10, , PAD_LEFT  , )
        oPrintPvt:SayAlign(nLinAtu, nColDescr ,  "BBBBBB",   oFontDet,  100,  10, , PAD_LEFT  , )
        oPrintPvt:SayAlign(nLinAtu, nColTipo  ,  "CC"    ,   oFontDet,  040,  10, , PAD_CENTER, )
        oPrintPvt:SayAlign(nLinAtu, nColTpDes ,  "DDDDDD",   oFontDet,  100,  10, , PAD_LEFT  , )
        oPrintPvt:SayAlign(nLinAtu, nColUnMed ,  "EE"    ,   oFontDet,  040,  10, , PAD_CENTER, )
        oPrintPvt:SayAlign(nLinAtu, nColUMDes ,  "FFFFFF",   oFontDet,  100,  10, , PAD_LEFT  , )
        nLinAtu += nEspLin

        //Faz uma linha de separação
        oPrintPvt:Line(nLinAtu-3, nColIni, nLinAtu-3, nColFin, nCorCinza)
    Next

    //Verifica a quebra de página
    fQuebra()

    //Encerra a última página
    fImpRod()
     
    //Se for via manual, mostra o relatório
    oPrintPvt:Preview()

Return NIL


Static Function fImpCab()

    nLinAtu   := 40

	//Inicializa a página
    oPrintPvt:StartPage()

    //Somente se for na primeira página
    If nPagAtu == 1

        //Impressão do box e das linhas
        nFimQuadr := nLinAtu + ((nEspLin*6) + 5)
        oPrintPvt:Box(nLinAtu, nColIni, nFimQuadr, nColFin)
        oPrintPvt:FillRect({nLinAtu + 1, nColIni + 1, nLinAtu + nEspLin - 1, nColFin - 1}, oBrush)
        oPrintPvt:Line(nLinAtu + nEspLin,           nColIni,       nLinAtu + nEspLin,           nColFin,       ) //Linha separando o título dos dados
        oPrintPvt:Line(nLinAtu + nEspLin,           nColIni + 195, nFimQuadr,                   nColIni + 195, ) //Coluna entre Logo e Textos
        oPrintPvt:Line(nLinAtu + nEspLin,           nColFin - 085, nFimQuadr,                   nColFin - 085, ) //Coluna entre Textos e QRCode
        oPrintPvt:Line(nLinAtu + (nEspLin * 2) + 2, nColIni + 200, nLinAtu + (nEspLin * 2) + 2, nColIni + 360, nCorForte) //Linha abaixo do texto principal

        cLogoRel := "\images\logo_fuabc.jpg"
        oPrintPvt:SayBitmap(nLinAtu + 20, nColIni + 35, cLogoRel, 111, 67) // 100 é a metade de 200, - 35 que é a metade da largura, dá 065
        
        //Imprimindo o QRCode
        cUrlSite := "https://fuabc.org.br"
        oPrintPvt:QRCode(nLinAtu + 90, nColFin - 79, cUrlSite, 75)
        
        oPrintPvt:SayAlign(nLinAtu, nColIni + 005, "Dados:"                           , oFontDetN , 200, 015, nCorForte, PAD_LEFT, )
        nLinAtu += nEspLin

        oPrintPvt:SayAlign(nLinAtu, nColIni + 200, "Fundação do ABC:"                 , oFontCabN , 200, 015, nCorForte, PAD_LEFT, )
        nLinAtu += nEspLin + 5

        oPrintPvt:SayAlign(nLinAtu, nColIni + 200, "Site:"                            , oFontDetN , 200, 015, , PAD_LEFT, )
        oPrintPvt:SayAlign(nLinAtu, nColIni + 270, "https://fuabc.org.br"             , oFontDet  , 200, 015, , PAD_LEFT, )
        nLinAtu += nEspLin

        oPrintPvt:SayAlign(nLinAtu, nColIni + 200, "eMail:"                           , oFontDetN , 200, 015, , PAD_LEFT, )
        oPrintPvt:SayAlign(nLinAtu, nColIni + 270, "renato.silva@fuabc.org.br"        , oFontDet  , 200, 015, , PAD_LEFT, )
        nLinAtu += nEspLin

        oPrintPvt:SayAlign(nLinAtu, nColIni + 200, "WhatsApp:"                        , oFontDetN , 200, 015, , PAD_LEFT, )
        oPrintPvt:SayAlign(nLinAtu, nColIni + 270, "(11) 9 9738-5495"                 , oFontDet  , 200, 015, , PAD_LEFT, )
        nLinAtu += nEspLin

        oPrintPvt:SayAlign(nLinAtu, nColIni + 200, "Um projeto da *RSilva Sistemas*"  , oFontDetI , 200, 015, , PAD_LEFT, )
        nLinAtu += nEspLin

        //Imprime textos laterais com o método Say
        cTextoAux := "At vero eos et accusamus et iusto odio dignissimos ducimus qui blanditiis praesentium voluptatum deleniti atque corrupti quos dolores et quas molestias excepturi sint occaecati cupiditate non provident"
        oPrintPvt:Say(040,     nColIni - 15, "Esq: " + cTextoAux, oFontMin, , nCorForte, 090)
        oPrintPvt:Say(nLinFin, nColFin + 15, "Dir: " + cTextoAux, oFontMin, , nCorForte, 270)

    EndIf

	nLinAtu += 010
    oPrintPvt:SayAlign(nLinAtu+05, nColProd  ,  "Produto"       ,  oFontMin,  060,  10, nCorCinza, PAD_LEFT  , )
    oPrintPvt:SayAlign(nLinAtu+05, nColDescr ,  "Descrição"     ,  oFontMin,  100,  10, nCorCinza, PAD_LEFT  , )
    oPrintPvt:SayAlign(nLinAtu+05, nColTipo  ,  "Tp."           ,  oFontMin,  040,  10, nCorCinza, PAD_CENTER, )
    oPrintPvt:SayAlign(nLinAtu+00, nColTpDes ,  "Tipo"          ,  oFontMin,  100,  10, nCorCinza, PAD_LEFT  , )
    oPrintPvt:SayAlign(nLinAtu+10, nColTpDes ,  "Descrição"     ,  oFontMin,  100,  10, nCorCinza, PAD_LEFT  , )
    oPrintPvt:SayAlign(nLinAtu+05, nColUnMed ,  "U.M."          ,  oFontMin,  040,  10, nCorCinza, PAD_CENTER, )
    oPrintPvt:SayAlign(nLinAtu+00, nColUMDes ,  "Unidade Medida",  oFontMin,  100,  10, nCorCinza, PAD_LEFT  , )
    oPrintPvt:SayAlign(nLinAtu+10, nColUMDes ,  "Descrição"     ,  oFontMin,  100,  10, nCorCinza, PAD_LEFT  , )
	
	//Linha Separatória
	nLinAtu += 020
	oPrintPvt:Line(nLinAtu, nColIni, nLinAtu, nColFin, )
	nLinAtu += 005
Return


Static Function fImpRod()

	Local nLinRod:= nLinFin + 10
	Local cTexto := ''

	//Linha Separatória
	oPrintPvt:Line(nLinRod,   nColIni, nLinRod,   nColFin, nCorFraca)
	nLinRod += 5
	
	//Dados da Esquerda
	cTexto := dToC(dDataGer)+"     "+cHoraGer+"     "+FunName()+" (zRel14)     "+cUserName
	oPrintPvt:SayAlign(nLinRod, nColIni, cTexto, oFontRod, 400, 10, nCorForte, PAD_LEFT, )
	
	//Direita
	cTexto := "Página "+cValToChar(nPagAtu)
	oPrintPvt:SayAlign(nLinRod, nColFin-40, cTexto, oFontRod, 040, 10, nCorForte, PAD_RIGHT, )
	
	//Finalizando a página e somando mais um
	oPrintPvt:EndPage()
	nPagAtu++
Return


Static Function fQuebra()
    //Se atingiu o limite, quebra de página
	If nLinAtu >= nLinFin-5
		fImpRod()
		fImpCab()
	EndIf
Return

